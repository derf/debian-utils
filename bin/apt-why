#!/usr/bin/env perl
## Copyright Â© 2008-2009 by Daniel Friesel <derf@derf.homelinux.org>
## License: WTFPL <http://sam.zoy.org/wtfpl>
use strict;
use warnings;
use AptPkg::Cache;
use Switch;
use Getopt::Long;

my $cache = AptPkg::Cache->new;
my $not = 0;
my @deptypes;
GetOptions(
	'not' => \$not,
	'deptype=s' => \@deptypes,
);
my $packagename = shift or die("No packagename given");
my @known;

@deptypes = split(/,/, join(',', @deptypes));

sub revdeps {
	my $name = shift;
	my $package = $cache->{$name};
	my @return;
	my $rdeps = $package->{RevDependsList};
	foreach (@$rdeps) {
		if (
			$cache->{$_->{ParentPkg}{Name}}->{CurrentState} eq 'Installed' and (
				(not @deptypes and $_->{DepType} !~ /^(Conflicts|Replaces|Obsoletes)$/) or
				(@deptypes and "$_->{DepType}" ~~ @deptypes)
			)
		) {
			next if ($_->{ParentPkg}{Name} ~~ @known);
			push(@return, $_->{ParentPkg}{Name});
		}
	}
	push(@known, @return);
	return(@return);
}

sub recurse {
	my $name = shift;
	my $level = (shift) + 1;
	printf("%s%s\n", "  " x ($level-1), $name);
	return if ($level >= 5);
	recurse($_, $level) foreach revdeps($name);
}

recurse($packagename, 0);

__END__

=head1 NAME

apt-why - filtered reverse dependency displayer using AptPkg::Cache

=head1 SYNOPSIS

B<apt-why> [ I<options> ] I<package>

=head1 DESCRIPTION

B<apt-why> recursively displays I<package>'s reverse dependency, thus
showing which packages require I<package> on the system.

=head1 OPTIONS

=over

=item B<--deptype>=I<types>

Only show reverse dependency which mach the specified I<types> (comma separated list).
Right now, possible types are Depends, Suggests, Recommends.

=head1 AUTHOR

Daniel Friesel <derf@derf.homelinux.org>

=head1 LICENSE

  0. You just DO WHAT THE FUCK YOU WANT TO.
