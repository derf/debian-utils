#compdef apt-why
## Completion for apt-why
## Daniel Friesel <derf@derf.homelinux.org>
## https://derf.homelinux.org/~derf/dotfiles/completion/_apt-why
## see also: https://derf.homelinux.org/~derf/code/apt-why

typeset -a arguments

function _apt-why_deptype {
	typeset prefix=''
	typeset -a keys used_prefixes
	keys=(Depends Recommends Suggests)
	if [[ -prefix *, ]] {
		prefix=${words[$CURRENT]%,*},
		used_prefixes=(${(s:,:)prefix})
	}
	_wanted expression expl 'dependency type' \
	compadd -F used_prefixes -qS , -P "$prefix" $keys
}

arguments=(
	'--all[consider all available packages]'
	'--depth=-[recursion depth]:recursion depth'
	'--deptype[filter by dependency type]:dependency type:_apt-why_deptype'
	'--tabwidth=-[spaces per recursion level]:spaces per recursion level'
	'*:package:_deb_packages "" avail'
)

_arguments -s $arguments
